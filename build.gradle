plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'com.qixalite.spongestart2' version '4.0.0'
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    shadow 'org.spongepowered:spongeapi:7.1.0'
    shadow files('libs/boop.jar')
    shadow files('libs/mcclans.jar')
    shadow 'org.apache.logging.log4j:log4j-core:2.8.1'

    compile 'io.github.nucleuspowered:nucleus-api:1.12.2-S7.1';
    compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile ('net.dv8tion:JDA:4.1.1_140') {
        exclude module: 'opus-java'
    }
    compile 'com.vdurmont:emoji-java:4.0.0'
}

spongestart{
    minecraft '1.12.2'
}

repositories {
    mavenCentral()
    jcenter()
    maven { url = "http://repo.drnaylor.co.uk/artifactory/list/minecraft" }
    maven { url = "https://raw.github.com/FabioZumbi12/UltimateChat/mvn-repo/" }
    maven { url 'https://jitpack.io' }
}

shadowJar {
    relocate('org.apache', 'lambda.org.apache') {
        exclude 'org/apache/logging/**'
    }
    relocate 'org.json', 'lambda.org.json'
    relocate 'gnu.trove', 'lambda.gnu.trove'
    relocate 'com.mashape.unirest', 'lambda.com.mashape.unirest'
    relocate 'com.squareup', 'lambda.com.squareup'
    relocate 'com.fasterxml', 'lambda.com.fasterxml'
    relocate 'okio', 'lambda.okio'
    relocate 'okhttp3', 'lambda.okhttp3'
    dependencies {
        exclude(dependency('org.slf4j:slf4j-api'))
        exclude(dependency('net.java.dev.jna:jna'))
    }

    exclude 'modules-info.java'
    exclude 'modules-info.class'
}

build.dependsOn shadowJar

sponge.plugin.id = pluginId
compileJava.options.encoding = 'UTF-8'
